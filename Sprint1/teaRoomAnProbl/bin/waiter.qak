System tearoom

//Messages **********************************************************************
//DIVISI PER CHI INVIA

//**********CLIENT

Request clientRingEntryRequest : clientEntryRequest(PAYLOAD)
Reply clientRingEntryReply : clientEntryReply(ID) //invia smartbell in realtà

Dispatch clientOrderReady : clientOrderReady(ID)
Dispatch clientOrder : clientOrder (ID, ORDER)
Dispatch clientPaymentReady : clientPaymentReady(ID)

//**********SMARTBELL
Request smartbellEntryRequest : smartbellEntryRequest(ID)
Reply smartbellEntryReply : smartbellEntryReply(ID)
Dispatch smartbellClientRejected : smartbellClientRejected(TEMP) //verso se stesso


//**********BARMAN
Dispatch barmanOrderReady : barmanOrderReady(ID) //= a tavolo o cliente id (per distinguere in futuro)


//**********WAITER
Dispatch waiterOrderForward : waiterOrderForward(ID, ORDER) //Order contenente ID cl. o tavolo (in futuro per distringuere)

//**********WAITER interni
Dispatch clean : clean(R)
Dispatch doneCleanRun : doneCleanRun(P)
Dispatch engineReady : engineReady(P)
Request moveTo : moveTo(X,Y)
Reply done : done(X,Y)
Dispatch end : end(V)
Request step : step( TIME )	
Reply   stepdone   : stepdone(V)  
Dispatch cmd : cmd(MOVE) 
Reply   stepfail   : stepfail(DURATION, CAUSE)
 

//**********
Dispatch initSimulation : initSimulation(P)

//end messages ******************************************************************* 
//context and QActors 
/*USE JSON FILE TO READ HOST AND PORT */
Context ctxtearoom ip [host="127.0.0.1" port=8040]
Context ctxbasicrobot ip [host="localhost" port=8020] 
ExternalQActor basicrobot context ctxbasicrobot 
//ExternalQActor waiterengine context ctxTearoom
//end context and Qactors 


QActor waitermind context ctxtearoom{
	//inizializzazione varibili
	[#		
		val DoClear = 1
		val DoSanitize = 2
		val DoClean = 3
		
		val CollectTime = 4000L
				 
		val X_home = "0"
		val Y_home = "0"
		
		val X_bar = "6"
		val Y_bar = "0"
		
		val X_entrance = "0"
		val Y_entrance = "4"
		
		val X_exit = "6"
		val Y_exit = "4"
		
		val X_table_1 = "2"
		val Y_table_1 = "2"
		
		val X_table_2 = "0"
		val Y_table_2 = "0"
		
		data class Table(var state: String ="tableClean") { }
		val table1 = Table()
		val table2 = Table()
		var stateTable1 = "tableClean"
		var stateTable2 = "tableClean"
		
		
	#]
	
	State startState initial{
		println("waitermind || START")
		updateResource [# "startState" #] 
	}
	Transition t0	whenMsg engineReady -> rest
	
	State rest{
		println("waitermind || rest")
		updateResource [# "rest"#]
	}//end rest
	Transition t1	whenRequest smartbellEntryRequest -> accept
					whenMsg clientOrderReady -> reachTableOrder
					whenMsg barmanOrderReady -> reachBar
					whenMsg end -> endState
					whenMsg clientPaymentReady -> reachTableCollect
					
	State reachHome{
		//[# readLine() #]
		println("waitermind || reachHome")
		updateResource [# "reachHome" #]
		request waiterengine -m moveTo : moveTo($X_home, $Y_home)
		
	}//end reachHome
	Transition t2	whenReply done -> rest
	
	State accept{
		//[# readLine() #]
		println("waitermind || accept")
		updateResource [# "accept" #]
		onMsg(smartbellEntryRequest : smartbellEntryRequest(ID)){
			replyTo smartbellEntryRequest with smartbellEntryReply : smartbellEntryReply(payloadArg(0))	
		}
				
	}//end accept
	Goto	reachDoor
	
	State reachDoor{
		//[# readLine() #]
		println("waitermind || reachDoor")
		updateResource [# "reachDoor" #]
		println("INVIO IL MOVETO")
		request waiterengine -m moveTo : moveTo($X_entrance, $Y_entrance)
		println("INVIATO IL MOVETO")
		
	}//end reachDoor
	Transition t3	whenReply done -> convoyTable
	
	State convoyTable{
		println("premere invio per veriricare posizione dopo reachDoor")
		updateResource [#""+itunibo.planner.plannerUtil.getPosX()+","+itunibo.planner.plannerUtil.getPosY()#]
		[# readLine() #]
		println("waitermind || convoyTable")
		updateResource [# "convoyTable"#]
		//updateResource [# "convoyTable"#]
		//attendo e prendo il cliente
		delay 500
		//STATO TAVOLO TO CHANGE
		//Controllare il tavolo libero
		request waiterengine -m moveTo : moveTo($X_table_1, $Y_table_1)
	}//end convoyTable
	Transition t4	whenReply done -> reachHome
	
	
	State reachTableOrder{ 
		//[# readLine() #]
		println("waitermind || reachTableOrder")
		updateResource [# "reachTableOrder"#]
		//check table ordering
		request waiterengine -m moveTo : moveTo($X_table_1, $Y_table_1)
		
	}//end reachTableOrder
	Transition t5	whenReply done -> waitOrderClient

	State waitOrderClient{ 
		println("premere invio per veriricare posizione al tavolo per prendere ordine")	
		updateResource [#""+itunibo.planner.plannerUtil.getPosX()+","+itunibo.planner.plannerUtil.getPosY()#]
		[# readLine() #] 
		println("waitermind || waitOrderClient")
		updateResource [# "waitOrderClient" #]
		
	}
	Transition t6	whenMsg clientOrder -> trasmit
	
	State trasmit {
		//[# readLine() #]
		println("waitermind || trasmit")
		updateResource [# "trasmit"#]	
		println("Messaggio per il barman")
		onMsg(clientOrder : clientOrder(ID, ORDER)){
			println("Invio messaggio barman")
			forward barman -m waiterOrderForward : waiterOrderForward(payloadArg(0), payloadArg(1))
			println("Inviato")
		}
		println("Messaggio inviato al barman?")
	}// end trasmit
	Goto reachHome
	
	State reachBar{
		//[# readLine() #]
		println("waitermind || reachBar")
		updateResource [# "reachBar"#]	
		//futuro distringuere in base a ID per il tavolo (secondo conoscenza stanza)
		request waiterengine -m moveTo : moveTo($X_bar, $Y_bar)
	}//end reachBar
	Transition t6	whenReply done -> serveTable
	
	State serveTable{
		println("premere invio per veriricare posizione dal barman")
		updateResource [#""+itunibo.planner.plannerUtil.getPosX()+","+itunibo.planner.plannerUtil.getPosY()#]
		[# readLine() #]
		println("waitermind || serveTable")
		updateResource [# "serveTable"#]	
		delay 1000
		request waiterengine -m moveTo : moveTo($X_table_1, $Y_table_1)
				
		//STATO DEL TAVOLO DA CAMBIARE
		[# table1.state = "tableDirty" #]
	}//end serveTable
	Transition t7	whenReply done -> reachHome
	
	State reachTableCollect{
		//[# readLine() #]
		println("waitermind || reachTableCollect")
		updateResource [# "reachTableCollect"#]
		//guardo il messaggio clientPaymentReady e leggo ID e capisco tavolo
		request waiterengine -m moveTo : moveTo($X_table_1, $Y_table_1)	
	}//end reachTableCollect
	Transition t8	whenReply done -> collect
		
	
	
	State collect{
		println("premere invio per veriricare posizione per collect")
		updateResource [#""+itunibo.planner.plannerUtil.getPosX()+","+itunibo.planner.plannerUtil.getPosY()#]
		[# readLine() #]
		println("waitermind || collect")
		updateResource [# "collect"#]
		//delay time perchè ci metto un po' a prendere il cash
		delayVar CollectTime
	}//end collect
	Goto convoyExit
	 
	State convoyExit{
		//[# readLine() #]
		println("waitermind || convoyExit")
		updateResource [# "convoyExit"#]
		
		request waiterengine -m moveTo : moveTo($X_exit, $Y_exit)	
		
	}//end convoyExit
	Transition t9 	whenReply done -> reachTableClean
	
	State reachTableClean{
		println("premere invio per veriricare posizione exit")
		updateResource [#""+itunibo.planner.plannerUtil.getPosX()+","+itunibo.planner.plannerUtil.getPosY()#]
		[# readLine() #]
		println("waitermind || reachTableClean")
		updateResource [# "reachTableClean"#]
		delay 1000
		//mi muovo verso il tavolo
		request waiterengine -m moveTo : moveTo($X_table_1, $Y_table_1)		
	}//end reachTableClear
	Transition t10 	whenReply done -> tableClearing
	

	State tableClearing{
		println("premere invio per veriricare posizione tavolo che sto pulendo")
		updateResource [#""+itunibo.planner.plannerUtil.getPosX()+","+itunibo.planner.plannerUtil.getPosY()#]
		[# readLine() #]
		println("waitermind || tableClearing")
		updateResource [# "tableClearing"#]
		
		[# table1.state = "tableClearing" #]
		
		forward waiterengine -m clean : clean($DoClear)

		//DICO ALL'ENGINE DI PULIRE
		//delayVar TableClearTime
		//STATO DEL TAVOLO DA CAMBIARE
		//[# table1.state = "tableCleared" #]

	}//end tableClear
	Transition t11	whenMsg doneCleanRun -> tableSanitizing
	
	
	State tableSanitizing{
		//[# readLine() #]
		println("waitermind || tableSanitizing")
		updateResource [# "tableSanitizing"#]
		
		[# table1.state = "tableSanitizing" #]
		
		forward waiterengine -m clean : clean($DoSanitize)
	}//end tableSanitize
	Transition t12	whenMsg doneCleanRun -> tableCleaning
	
	State tableCleaning{
		//[# readLine() #]
		println("waitermind || tableCleaning")
		updateResource [# "tableCleaning"#]
		
		[# table1.state = "tableCleaning" #]
		
		forward waiterengine -m clean : clean($DoClean)
	}//end tableClean
	Transition t12	whenMsg doneCleanRun -> updateTableCleaned
	
	State updateTableCleaned{
		//[# readLine() #]
		println("waitermind || updateTableCleaned")
		updateResource [# "updateTableCleaned" #]
		
		[# table1.state = "tableCleaned" #]
	}
	Goto	reachHome
	
	
	
	State endState{
		println("waitermind || TERMINATES")
		forward waiterengine -m end : end(V)
		terminate 0
	}//end endState
	
	
	
}
 


QActor smartbell context ctxtearoom{
	[#
		val Temp_max = 42
		var ID_client = 1
		var N_client_rejected = 0
	#]

	State s0 initial{
		println("smartbell || START")
		updateResource [# "START" #]
	}
	Goto waitRing
	
	State waitRing{
		println("smartbell || waitRing")
		updateResource [# "waitRing" #]		
	}
	Transition t0 	whenRequest clientRingEntryRequest -> checkTempClient
					whenMsg end -> endState
	
	State checkTempClient{
		[#  var ClientTemp = Math.random()*6+35#] //random temperature
		println("smartbell || checkTempClient")
		updateResource [# "checkTempClient" #]	
		if [# ClientTemp < Temp_max #]
		{
			println("smartbell || clienteAccettatoDaSmartBell || temperatura = $ClientTemp")
			request waitermind -m smartbellEntryRequest : smartbellEntryRequest($ID_client)  
			[# ID_client++ #]
		}
		else
		{
			println("smartbell || clienteRifiutatoDaSmartBell || temperatura = $ClientTemp")
			forward smartbell -m smartbellClientRejected : smartbellClientRejected($ClientTemp) //per questione statistica
			
		}
	}
	Transition t1 	whenReply 	smartbellEntryReply -> checkWaiterReply	
					whenMsg 	smartbellClientRejected -> clientRejected
	
	State checkWaiterReply{
		onMsg(smartbellEntryReply : smartbellEntryReply(ID)){
			replyTo clientRingEntryRequest with clientRingEntryReply : clientRingEntryReply(payloadArg(0)) 
		}
	}
	Goto waitRing
	
	State clientRejected{
		[# N_client_rejected++ #]
		onMsg(smartbellClientRejected : smartbellClientRejected(TEMP)){ //rispondo con temperatura (maggior del normale)
			replyTo clientRingEntryRequest with clientRingEntryReply : clientRingEntryReply(payloadArg(0)) 
		}
	}
	Goto waitRing
	
	State endState{
		println("smartbell || END")
		terminate 0
	}
	
}



QActor waiterengine context ctxtearoom{ 
	
		[# 
			var StepTime = 445L
			val BackTime     = 2 * StepTime / 3
			
			val mapRoom  = "teaRoomExplored"
			var XPoint = "0"
			var YPoint = "0"
			
			val TableClearTime = 4000L
			val TableSanitizeTime = 2000L
			val TableCleanTime = 2000L
			
			var CmdToPerform = ""  
		#]
		
	State s0 initial{
		println("waiterengine || START")
		updateResource [# "startState" #]
		
	
		run itunibo.planner.plannerUtil.initAI() 
		run itunibo.planner.plannerUtil.loadRoomMap(mapRoom) 		
		run itunibo.planner.plannerUtil.showCurrentRobotState()	
		
		forward waitermind -m engineReady : engineReady(P)
	}
	Goto waitCmd
	
	
	State waitCmd{
		println("waiterengine || waitCmd")
		updateResource [# "waitCmd" #]
	}
	Transition t0	whenRequest moveTo -> planPath
					whenMsg clean -> cleanTable
	
	
	
	State planPath{
		println("waiterengine || planPath")
		updateResource [# "planPath" #]
		onMsg(moveTo : moveTo(X,Y)){
			[# XPoint = payloadArg(0)
			   YPoint = payloadArg(1)			  
			 #]
			 }
			println("moveTo ($XPoint,$YPoint)")
			run itunibo.planner.plannerUtil.planForGoal("$XPoint","$YPoint")
	}
	Goto readStep
	
	State readStep{
//		println("waiterengine || readStep")
			[#  CmdToPerform = itunibo.planner.plannerUtil.getNextPlannedMove() #]
	}
	Goto execStep if  [# CmdToPerform == "w"#] else execMove
	//funziona? se mi trovo già al punto che succede?
	
	State execMove{
//		println("waiterengine || execMove")
		forward basicrobot -m cmd : cmd($CmdToPerform)
		delay 200
	}
	Goto updateMap
	
	
	State endPath{
		println("waiterengine || endPath")
		updateResource [# "endPath" #]
		println("done moveTo($XPoint,$YPoint)")
		run itunibo.planner.plannerUtil.showCurrentRobotState()
		replyTo moveTo with done : done($XPoint,$YPoint)
	}
	Goto waitCmd
	
	State execStep{
//		println("waiterengine || execStep")
		request basicrobot -m step : step($StepTime)
	}
	Transition t1	whenReply stepdone -> updateMap
					whenReply stepfail   -> errorHandler
	
	State updateMap{
		updateResource [# itunibo.planner.plannerUtil.getMapOneLine() #]
		run itunibo.planner.plannerUtil.updateMap("$CmdToPerform")	
		
	}
	Goto readStep if [# CmdToPerform.length > 0 #] else endPath
	
	State errorHandler{
		println("waiterengine | errorHandler")
		//torno indietro di un po'
		
		onMsg(stepfail : stepfail(DURATION, CAUSE) ){
			[# val D = payloadArg(0).toLong()  ; val Dt = Math.abs(StepTime-D); val BackT = D/2 #] 
			println("robotmapper stepFail D= $D, BackTime = ${BackTime} BackT=$BackT")
 			if [# D > BackTime #] {    
				forward basicrobot -m cmd : cmd( s )
				delayVar BackT
				forward basicrobot -m cmd : cmd( h )
			}	
//	 		if [# ! itunibo.planner.plannerUtil.atHome() #]{
	 			run itunibo.planner.plannerUtil.updateMapObstacleOnCurrentDirection()
//	 		}
	 		run itunibo.planner.plannerUtil.showCurrentRobotState()
			updateResource [# "stepFail" #]
			delay 500	
		}	    
	}
	Goto readStep if [# CmdToPerform.length > 0 #] else endPath
	

	State cleanTable{
		onMsg(clean : clean(R)){
			if [# payloadArg(0) == "1" #] 
			{//sono in clear
				delayVar TableClearTime
			}//else if
			if [# payloadArg(0) == "2" #]
			{
				delayVar TableSanitizeTime
			}
			if [# payloadArg(0) == "3" #]
			{
				delayVar TableCleanTime
			}
		}
		forward waitermind -m doneCleanRun : doneCleanRun(P)
	}
	Goto waitCmd
	
	
}

QActor barman context ctxtearoom{
	[#
		val TimePrepareOrder = 2000L
	#]
	State s0 initial{
		println("barman || START")
		updateResource [# "START" #]
	}
	Goto waitOrder
	
	State waitOrder{
		println("barman || waitOrder")
		updateResource [# "waitOrder" #]
		
	}
	Transition t0 	whenMsg waiterOrderForward -> prepareOrder
					whenMsg end -> endState
	
	State prepareOrder{ 
		println("barman || prepareOrder")
		updateResource [# "prepareOrder" #]
		delayVar TimePrepareOrder
		onMsg(waiterOrderForward : waiterOrderForward(ID,ORDER)){
			println("Order:  ${payloadArg(1)}")
			forward waitermind -m barmanOrderReady : barmanOrderReady(payloadArg(0))
		}		
	}
	Goto waitOrder
	
	State endState{
		println("barman || END")
		terminate 0
	}
}







